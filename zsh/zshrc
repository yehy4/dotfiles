# Keep 1 million lines of history.
HISTFILE=~/.zsh_history
HISTSIZE=1000000
SAVEHIST=1000000

# Decrease timeout waiting for key combinations (default is 40).
KEYTIMEOUT=1

# Use neovim as the default editor.
export EDITOR=nvim

# Share history among concurrent shells.
setopt share_history
# Record timestamp of command in HISTFILE.
setopt extended_history
# Delete duplicates first when HISTFILE size exceeds HISTSIZE.
setopt hist_expire_dups_first

# Correct mis-spelled commands.
setopt correctall

# Disable terminal beeps during completion.
unsetopt list_beep

# Show completion menu if there is more than one match.
zstyle ':completion:*' menu select=1
# Insert the full unambiguous prefix in a term before triggering the completion menu.
zstyle ':completion:*' insert-unambiguous true
# Use colors when displaying the completion menu for files and directories.
eval `dircolors` # Set LS_COLORS environment variable
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
# Enable case-insensitive completion.
zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]} m:{[:lower:][:upper:]}={[:upper:][:lower:]}'
autoload -Uz compinit && compinit
zmodload zsh/complist

# Load version control information.
# See https://zsh.sourceforge.io/Doc/Release/User-Contributions.html#Version-Control-Information.
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git svn
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '|S'
zstyle ':vcs_info:*' unstagedstr '|U'
# Format the vcs_info_msg_0_ variable.
zstyle ':vcs_info:git*' formats "%f(%F{#88C0D0}î‚ %b%f%F{#EBCB8B}%a%m%u%c%f) "
precmd() { vcs_info }

# Set the prompt.
setopt prompt_subst # Allow substitutions in the prompt to be able to use variables.
PROMPT='[%F{#D08F70}%?%f] ${vcs_info_msg_0_}$ '
RPROMPT='%F{#88C0D0}%~'

# Use vi keybindings.
bindkey -v
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history

# Search history by navigating up and down in vi normal mode.
bindkey -M vicmd 'k' history-beginning-search-backward
bindkey -M vicmd 'j' history-beginning-search-forward

source `dirname $0`/zsh-autosuggestions/zsh-autosuggestions.zsh
source `dirname $0`/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

alias ls='ls --color'
alias gitlog='git log --all --graph --decorate --oneline'

# Attach to a (newly-created or existing) tmux session called "main" if not
# already inside a tmux session, and the terminal is not gnome-terminal (to
# accomodate "open in terminal" when right clicking in Nautilus/Gnome Files).
if [ -z "$TMUX" ] && ps -o cmd= -p $(ps -o ppid= -p $$) | grep -v -q gnome-terminal; then
  tmux new-session -A -s main
fi
